@PACKAGE_INIT@

set(websocket-server_DEPENDENCIES websocket http-server)
set(websocket-client_DEPENDENCIES websocket http-client)

set(http-server-express_DEPENDENCIES http-server)

set(http-server_DEPENDENCIES http)
set(http-client_DEPENDENCIES http)

set(net-in_DEPENDENCIES net)
set(net-in6_DEPENDENCIES net)
set(net-l2_DEPENDENCIES net)
set(net-rc_DEPENDENCIES net)
set(net-un_DEPENDENCIES net)

set(net-in-stream_DEPENDENCIES net-in)
set(net-in6-stream_DEPENDENCIES net-in6)
set(net-l2-stream_DEPENDENCIES net-l2)
set(net-rc-stream_DEPENDENCIES net-rc)
set(net-un-stream_DEPENDENCIES net-un)

set(db-mariadb_DEPENDENCIES core)

set(core-socket_DEPENDENCIES core)

set(core-socket-stream_DEPENDENCIES core-socket)

set(core-socket-stream-legacy_DEPENDENCIES core-socket-stream)
set(core-socket-stream-tls_DEPENDENCIES core-socket-stream)

set(core_DEPENDENCIES mux-@IO_Multiplexer@ utils logger)

macro(LOAD_COMPONENT_WITH_DEPENDENCIES _comp)
    message("Loading component: " ${_comp})
    include("${CMAKE_CURRENT_LIST_DIR}/snodec${_comp}Options.cmake" OPTIONAL
            RESULT_VARIABLE ${_comp}_OPTIONS_FILE
    )
    if(NOT "${${_comp}_OPTIONS_FILE}" STREQUAL "NOTFOUND")
        message("  " ${_comp} ": Options loaded from " ${${_comp}_OPTIONS_FILE})
    endif()
    foreach(_depcomp ${${_comp}_DEPENDENCIES})
        if("${${_depcomp}_LOADED}" STREQUAL "")
            message("  " ${_comp} " depends on " ${_depcomp} " ... loading")
            load_component_with_dependencies(${_depcomp})
        else()
            message("  " ${_comp} " depends on " ${_depcomp}
                    " ... already loaded"
            )
        endif()
    endforeach()
    include("${CMAKE_CURRENT_LIST_DIR}/snodec_${_comp}_Targets.cmake")
    set(${_comp}_LOADED true)
endmacro()

set(_supported_components
    core
    core-socket-stream-legacy
    core-socket-stream-tls
    net-in-stream
    net-in6-stream
    net-rc-stream
    net-l2-stream
    net-un-stream
    db-mariadb
    http
    http-server
    http-client
    http-server-express
    websocket-server
    websocket-client
)

set(snodec_FOUND True)
foreach(_comp ${snodec_FIND_COMPONENTS})
    if(NOT _comp IN_LIST _supported_components)
        set(snodec_FOUND False)
        set(snodec_NOT_FOUND_MESSAGE "Unsupported component: ${_comp}")
    else()
        load_component_with_dependencies(${_comp})
    endif()
endforeach()
